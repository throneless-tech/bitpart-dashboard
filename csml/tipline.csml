// This is a template. It will not work before being processed by the system


start:
// if bitpart receives passcode, set admin
  remember tip = ""
  do passcode = event
  do passcode.to_uppercase()

  if (passcode.contains("[passcode]")) {
    // set _whisperable var in memory in a separate step FIXME
    remember _whisperable = true

    say "Welcome, you are now set up as a user who can receive messages from people who have asked for human support. You will receive messages from users as they come in. Otherwise, send a message that says 'help' at any time to exit the conversation or delete your data."
    hold

    if (event.to_lowercase() == "help") {
      goto admin_interface_step
    }
  }
  // if _whisperable is set, user is already an admin
  if (event._whisperable == true) {
    goto admin_interface_step
  }
  else 
    // conversation is triggered by a message from a user who says they have a tip
    // TODO Bitpart interprets incoming language. Response is in the same language
    say Text("Thank you for contacting [name].
    
This is our secure tipline. Messages are visible to our staff. Any information you share is stored on our systems.
    
For more information at any time, including how to delete your data, send a message with the words 'My data'.")

    say Text("Even though this is a secure tipline, please do not share highly sensitive information here.
    
Please share your tip with us.")
    hold

    if (event.to_lowercase() == "my data") {
      goto privacy_policy_step
    } else { 
      do tip = event  
      
      goto tip_reply_step
    }

admin_interface_step:
  say Text("What would you like to do? Enter a number:

1. Receive messages
2. Exit this conversation
3. Delete my data")
  hold

  if (event == 1) {
    goto admin_interface_step
  } else if (event == 2) {
    say "I am now closing this conversation. Please send the passcode again if you need me."

    goto end
  } else if (event == 3) {
    goto delete_me_confirm_step
  } else if (event.to_lowercase() == "help") {
    goto admin_interface_step
  } else {
    say "Sorry, I did not get that. Please select an option from the list."

    goto admin_interface_step
  }

delete_me_confirm_step:
  say Text("You have asked that you be removed from the conversation and all your data be deleted from the system.
  
Once this happens, you will no longer be able to receive messages from this number, unless you send a message to it again.
  
Reply with 'Delete' to confirm.
  
Reply with 'Go back' to go back to the beginning.")
  hold

  if (event.to_lowercase() == "delete") {
    goto confirm_delete_step
  } else if (event.to_lowercase() == "go back") {
    goto admin_interface_step
  } else
    say "Sorry, I did not get that."
    do count += 1
    goto delete_me_confirm_step

privacy_policy_step:
  say Text("My data
  
[privacyPolicy]")
  say Text("Reply with 'Agree' and continue to the main menu.
  
Reply with 'Delete' to delete your data.")
  hold

  // Agree and return to menu, disagree and delete data
  if (event.to_lowercase() == "agree") {
    say "Even though this is a secure tipline, please do not share highly sensitive information here.
    
Please share your tip with us."
    hold

    do tip = event  
    
    goto tip_reply_step

  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else
    say "Sorry, I did not get that."
    goto privacy_policy_step

delete_data_step:
  say Text("You have asked that all your data be deleted from the system.
  
Once this happens, you will no longer receive messages from this number, unless you send a message to it again.
  
Reply with 'Delete' to confirm.
  
Reply with 'Go back' to go back to the data rights.")
  hold

  if (event.to_lowercase() == "delete") {
    goto confirm_delete_step
  } else if (event.to_lowercase() == "go back") {
    goto privacy_policy_step
  } else
    say "Sorry, I did not get that."
    goto delete_data_step

confirm_delete_step:
  say "All your data will now be deleted and you will no longer receive messages."
  delete
  goto end

tip_reply_step:
  say "Thank you. Our team will look into your tip and assess its suitability for our outlet.
  
We may want to follow up with you. Do you give permission for one of our journalists to contact you? You can reply with yes or no."
  hold

  if (event.to_lowercase() == "yes") {
    remember permission = event
    goto contact_method_step
  } else if (event.to_lowercase() == "no") {
    say "Ok, we will not contact you. Thanks again for the tip."

    whisper "A user sent the following anonymous tip: {{tip}}.
    
They did not wish to be contacted."

    goto end
  } else if (event.to_lowercase() == "my data") {
    goto privacy_policy_step
  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else {
    say "Sorry, our system didn't understand what you meant. Please try again."
    goto tip_reply_step
  }

contact_method_step:
  remember contact_preference = ""

  say ("Thank you. How do you prefer we contact you? Please choose the number that corresponds with your preferred method.
  
1. Signal call
2. Signal text message
3. Phone call
4. Any
5. None, don't contact me")
  hold

  if (event == 1) {
    do contact_preference = "signal call"
    goto record_number_step    
  } else if (event == 2) {
    do contact_preference = "signal text message"
    goto record_number_step    
  } else if (event == 3) {
    do contact_preference = "phone call"
    goto record_number_step    
  } else if (event == 4 || event == 5) {
    goto confirm_contact_method_step
  } else if (event.to_lowercase() == "my data") {
    goto privacy_policy_step
  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else {
    say "Sorry, I did not get that. Please choose a number from the list."
    goto contact_method_step
  }

record_number_step:
  say "Thank you. Your preference is noted. Please share your preferred phone number."
  hold

  remember contact_number = event

  say "Thanks, we will send your information to our team. Please message us again if you have another tip."
  
  whisper "A user sent a tip and provided the following contact information: Via {{contact_preference}}, contact {{contact_number}}.
  
Here is the tip they provided: {{tip}}"

  goto end

confirm_contact_method_step:
  say Text("Reply 'Yes' to confirm we can use any method to contact you.
  
Reply 'Back' to go to the previous menu of options
    
Reply 'No' if you prefer to not be contacted.")
  hold

  if (event.to_lowercase() == "yes") {
    say "Thank you. Your preference is noted."

    whisper "A user sent a tip and provided the following contact information: Via any method, contact {{contact_number}}.
    
Here is the tip they provided: {{tip}}"

    // TODO SACHA do we accept another message after this?
  } else if (event.to_lowercase() == "no") {
    say "Ok, we will not contact you. Thanks again for the tip."
    delete
    goto end
  } else if (event.to_lowercase() == "back") {
    goto contact_method_step
  } else if (event.to_lowercase() == "my data") {
    goto privacy_policy_step
  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else {
    say "Sorry, I did not get that."
    goto confirm_contact_method_step
  }

  goto end