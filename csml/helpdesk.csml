// This is a template. It will not work before being processed by the system


start:
  // if bitpart receives passcode, set admin
  do passcode = event.to_uppercase()

  if (passcode.contains("[passcode]")) {
    // set _whisperable var in memory
    remember _whisperable = true

    say Text("Welcome, you are now set up as a bot administrator who can receive helpdesk messages from people who have asked for human support. You will receive messages from bot users as they come in. Otherwise, send a message that says 'Help' at any time to exit the conversation or delete your data.")
    hold

    if (event.to_lowercase() == "help") {
      goto admin_interface_step
    }
  }
  // if _whisperable is set, user is already an admin
  if (_whisperable == true) {
    goto admin_interface_step
  }
  else 
    remember count = 0

    // TODO Bitpart interprets incoming language. Response is in the same language
    say Text("Thank you for contacting [name]. [description] We use a secure automated messaging system so you can get quicker answers to problems, and our team is on hand to help.

If you are in immediate physical danger contact [referral].")

    // TODO: user research needed about if we want bitpart to accept voice notes
    say Text("Our helpdesk will ask you to select the problem you're experiencing.

[storageAccess]

We will not ask you for personal information.

For more information at any time, including how to delete your data, you can reply with 'My data'.

To continue, reply with 'Agree'.

To disagree and end the conversation, reply with 'End'.")
    hold

    if (event.to_lowercase() == "my data") {
      goto privacy_policy_step
    } else if (event.to_lowercase() == "agree") {
      goto menu_step
    } else if (event.to_lowercase() == "end") {
      goto delete_data_step
    } else if (event.match_regex("[Human|human|person|Person|Talk|talk]")) {
      // TODO: is there a better way to check if the user wants to talk to a real person?
      say "Ok. A member of our helpdesk team will follow up as soon as they can."
      goto incomplete_step
    } else {
      say "Sorry,  our system didn't understand what you meant. A member of our helpdesk team will be in touch with you as soon as possible."
      goto incomplete_step
    }

admin_interface_step:
  say Text("What would you like to do? Enter a number:
  
1. Exit this conversation
2. Delete my data")
  hold

  if (event == 1) {
    say "I am now closing this conversation. Please send a message again if you need me."

    goto end
  } else if (event == 2) {
    goto delete_me_confirm_step
  } else if (event.to_lowercase() == "help") {
    goto admin_interface_step
  } else {
    say "Sorry, I did not get that. Please select an option from the list."

    goto admin_interface_step
  }

delete_me_confirm_step:
  say Text("You have asked that you be removed from the conversation and all your data be deleted from the system.
  
Once this happens, you will no longer be able to receive messages from this number, unless you send a message to it again.

Reply with 'Delete' to confirm.

Reply with 'Go back' to go back to the beginning.")
  hold

  if (event.to_lowercase() == "delete") {
    goto confirm_delete_step
  } else if (event.to_lowercase() == "go back") {
    goto admin_interface_step
  } else
    say "Sorry, I did not get that."
    remember count = count + 1
    goto delete_me_confirm_step

confirm_delete_step:
  say "All your data will now be deleted and you will no longer receive messages."
  delete
  goto end

privacy_policy_step:
  if (count >= 3) {
    goto human_support_step
  }
  say Text("My data
  
[privacyPolicy]")
  say Text("Reply with 'Agree' and continue to the main menu
  
Reply with 'Delete' to delete your data.")
  hold

  // Agree and return to menu, disagree and delete data
  if (event.to_lowercase() == "agree") {
    goto menu_step
  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else
    say "Sorry, I did not get that."
    remember count = count + 1
    goto privacy_policy_step

delete_data_step:
  if (count >= 3) {
    goto human_support_step
  }

  say Text("You have asked that you be removed from the conversation and all your data be deleted from the system.
  
Once this happens, you will no longer receive messages from this number, unless you send a message to it again.

Reply with 'Delete' to confirm.

Reply with 'Go back' to go back to the menu.")
  hold

  if (event.to_lowercase() == "delete") {
    goto confirm_delete_step
  } else if (event.to_lowercase() == "go back" || event.to_lowercase() == "menu") {
    goto menu_step
  } else
    say "Sorry, I did not get that."
    remember count = count + 1
    goto delete_data_step

incomplete_step:
  // bitpart tags as 'incomplete' and flags for helpdesk team (human) attention (i.e. adds to a queue)
  whisper "Bitpart did not understand this user: {{event.client.user_id}}"
  
  goto end

menu_step:
  if (count >= 3) {
    say "Sorry, I did not get that."
    goto human_support_step
  }
  
  say Text("Menu
  
What issue are you seeking help with?

[problems]
[problems.length]. Other

Reply with the number.")
  hold

  [problems.solutions]
  else if (event == [problems.length]) {
    goto human_support_step
  }
  else if (event.to_lowercase() == "menu") {
    goto menu_step
  } else if (event.to_lowercase() == "my data") {
    goto privacy_policy_step
  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else {
    remember count = count + 1
    say "Sorry, our system didn't understand what you meant. Please select the most appropriate option:"
    goto menu_step
  }

check_if_solved_step:
  if (count >= 3) {
    goto human_support_step
  }

  say Text("Did that solve your problem?

Reply 'Yes' or 'No'.")
  hold

  if (event.to_lowercase() == "yes") {
    goto menu_options_step
  } else if (event.to_lowercase() == "no") {
    goto human_support_step
  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else {
    say "Sorry, I did not get that."
    remember count = count + 1
    goto check_if_solved_step
  }

menu_options_step:
  if (count >= 3) {
    say "Sorry, I did not get that."
    goto human_support_step
  }
  
  say Text("Glad that helped.
  
Reply with 'Menu' to return to the main menu and ask further questions. Reply with 'Delete' to delete your data from our system. Reply with 'End' to end this conversation.
  
Remember that if this is a sensitive conversation you can delete the conversation from the Signal app.")
  hold 

  if (event.to_lowercase() == "menu") {
    goto menu_step
  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else if (event.to_lowercase() == "my data") {
    goto privacy_policy_step
  } else if (event.to_lowercase() == "end") {
    say "Please contact us again if you need us."
    goto end
  } else {
    say "Sorry, I did not get that."
    remember count = count + 1
    goto check_if_solved_step
  }

human_support_step:
  say "We've understood that you have a question not covered here. A member of our helpdesk team will follow up as soon as they can to offer you support. We will aim to be in touch with you within [responseTime].

Please share what you need help with and a member of our team will get back to you."

  hold

  remember problem = event

  if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else if (event.to_lowercase() == "my data") {
    goto privacy_policy_step
  } else if (event.to_lowercase() == "end") {
    say "Please contact us again if you need us."
    goto end
  } else {
    goto contact_method_step
  }
  
  // goto end // we are not ending the conversation, uncomment if that changes

contact_method_step:
  say ("Thank you. How do you prefer we contact you? Please choose the number that corresponds with your preferred method.
  
1. Signal call
2. Signal text message
3. Phone call
4. Any
5. None, don't contact me")

  hold

  if (event == 1) {
    remember contact_preference = "signal call"
    goto record_number_step    
  } else if (event == 2) {
    remember contact_preference = "signal text message"
    goto record_number_step    
  } else if (event == 3) {
    remember contact_preference = "phone call"
    goto record_number_step    
  } else if (event == 4 || event == 5) {
    goto confirm_contact_method_step
  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else if (event.to_lowercase() == "end") {
    say "Please contact us again if you need us."
    goto end
  } else {
    say "Sorry, I did not get that. Please choose a number from the list."
    goto contact_method_step
  }

record_number_step:
  say "Thank you. Your preference is noted. Please share your preferred phone number or Signal username, if applicable."
  hold

  remember contact_number = event

  // flag relevant data to bitpart
  whisper "A user had a problem not covered by the helpdesk, and provided the following contact information: Via {{contact_preference}}, contact {{contact_number}}.
  
Here is the problem they experienced: {{problem}}"

  say "Thank you. An admin will continue this conversation. Please note that it might take some time for them to get in touch."

  goto end

confirm_contact_method_step:
  say Text("Reply 'Yes' to confirm we can use any method to contact you.
  
Reply 'Back' to go to the previous menu of options
    
Reply 'No' if you prefer to not be contacted.")
  hold

  if (event.to_lowercase() == "yes") {
    say "Thank you. Your preference is noted."

    whisper "A user had a problem not covered by the helpdesk, and provided the following contact information: Via any method, contact {{contact_number}}.
    
Here is the problem they experienced: {{problem}}"

    // TODO SACHA do we accept another message after this?
  } else if (event.to_lowercase() == "no") {
    say "Ok, we will not contact you. Thanks again for the heads up."

    whisper "A user had a problem not covered by the helpdesk: {{problem}}.
    
They sent this problem anonymously and did not wish to be contacted."

    goto end
  } else if (event.to_lowercase() == "back") {
    goto contact_method_step
  } else if (event.to_lowercase() == "my data") {
    goto privacy_policy_step
  } else if (event.to_lowercase() == "delete") {
    goto delete_data_step
  } else {
    say "Sorry, I did not get that."
    goto confirm_contact_method_step
  }

  // goto end // we are not ending the conversation, uncomment if that changes
